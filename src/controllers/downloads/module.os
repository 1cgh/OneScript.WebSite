#Использовать "../../model"

Перем ИдентификаторСекции;
Перем Заголовок;

Функция Index() Экспорт
	
	СписокСборок = УправлениеКонтентом.ПолучитьСписокСборок();	
	ПараметрыПредставления = Новый Структура;
	ПараметрыПредставления.Вставить("СписокСборок", СписокСборок);
	ПараметрыПредставления.Вставить("АктуальнаяСборка", УправлениеКонтентом.ПолучитьСоставСборки("latest"));
	ПараметрыПредставления.Вставить("НочнаяСборка", УправлениеКонтентом.ПолучитьСоставСборки("night-build"));
	
	ПараметрыПредставления.Вставить("ЛокальнаяНавигация", Новый Массив);
	
	ЛокальныеПараметры = Новый ЛокальныеПараметрыСтраницы(Заголовок, Истина, "/downloads/");
	ПараметрыПредставления.ЛокальнаяНавигация.Добавить(ЛокальныеПараметры);
	
	Возврат Представление("index", ПараметрыПредставления);
	
КонецФункции

Функция Archive() Экспорт
	
	Сборка = ЭтотОбъект.ЗначенияМаршрута.Получить("build");	
	Если Сборка = Неопределено Тогда
		Возврат ПеренаправлениеНаДействие("page404", "home");
	КонецЕсли;
	
	СодержимоеСтраницы = УправлениеКонтентом.ПолучитьСодержимоеСтраницыКонтента(ИдентификаторСекции, Сборка);
	ТекущаяСборка = УправлениеКонтентом.ПолучитьСоставСборки(Сборка);
	
	ПараметрыПредставления = Новый Структура;
	ПараметрыПредставления.Вставить("Содержимое", СодержимоеСтраницы.Значение);
	ПараметрыПредставления.Вставить("ТекущаяСборка", ТекущаяСборка);
	
	ПараметрыПредставления.Вставить("ЛокальнаяНавигация", Новый Массив);
	
	ЛокальныеПараметры = Новый ЛокальныеПараметрыСтраницы(Заголовок, Ложь, "/downloads/");
	ПараметрыПредставления.ЛокальнаяНавигация.Добавить(ЛокальныеПараметры);
	
	Если ТекущаяСборка <> Неопределено Тогда
		ЛокальныеПараметры = Новый ЛокальныеПараметрыСтраницы("Версия " + СтрЗаменить(ТекущаяСборка.Версия, "_", "."), Истина);
		ПараметрыПредставления.ЛокальнаяНавигация.Добавить(ЛокальныеПараметры);
	КонецЕсли;
	
	Возврат Представление("build-detail", ПараметрыПредставления);
	
КонецФункции

Функция Getfiledata() Экспорт
	
	Сборка = ЭтотОбъект.ЗначенияМаршрута.Получить("build");	
	Идентификатор = ЭтотОбъект.ЗначенияМаршрута.Получить("id");
	
	ПутьКФайлу = УправлениеКонтентом.ПолучитьПутьКФайлуСборки(Сборка, Идентификатор);
	Если ФС.ФайлСуществует(ПутьКФайлу) Тогда
		Возврат Файл(ПутьКФайлу, "application/octet-stream", Идентификатор)	
	Иначе
		Возврат ПеренаправлениеНаДействие("page404", "home");
	КонецЕсли;
	
КонецФункции

ИдентификаторСекции = "archive";
Заголовок = "Скачать";