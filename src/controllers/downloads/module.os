#Использовать "../../model"

Перем ИдентификаторСекции;

Функция Index() Экспорт
	
	СписокСборок = УправлениеКонтентом.ПолучитьСписокСборок();	
	ПараметрыПредставления = Новый Структура;
	ПараметрыПредставления.Вставить("СписокСборок", СписокСборок);
	ПараметрыПредставления.Вставить("АктуальнаяСборка", УправлениеКонтентом.ПолучитьСоставСборки("latest"));
	ПараметрыПредставления.Вставить("НочнаяСборка", УправлениеКонтентом.ПолучитьСоставСборки("night-build"));
	
	Возврат Представление("index", ПараметрыПредставления);
	
КонецФункции

Функция Archive() Экспорт

	Сборка = ЭтотОбъект.ЗначенияМаршрута.Получить("build");	
	Если Сборка = Неопределено Тогда
		Возврат ПеренаправлениеНаДействие("page404", "home");
	КонецЕсли;

	СодержимоеСтраницы = УправлениеКонтентом.ПолучитьСодержимоеСтраницыКонтента(ИдентификаторСекции, Сборка);

	ПараметрыПреставления = Новый Структура;
	ПараметрыПреставления.Вставить("Содержимое", СодержимоеСтраницы.Значение);
	ПараметрыПреставления.Вставить("ТекущаяСборка", УправлениеКонтентом.ПолучитьСоставСборки(Сборка));

	Возврат Представление("build-detail", ПараметрыПреставления);

КонецФункции

Функция Getfiledata() Экспорт
	
	Сборка = ЭтотОбъект.ЗначенияМаршрута.Получить("build");	
	Идентификатор = ЭтотОбъект.ЗначенияМаршрута.Получить("id");
	
	ПутьКФайлу = УправлениеКонтентом.ПолучитьПутьКФайлуСборки(Сборка, Идентификатор);
	Если ФС.ФайлСуществует(ПутьКФайлу) Тогда
		Возврат Файл(ПутьКФайлу, "application/octet-stream", Идентификатор)	
	Иначе
		Возврат ПеренаправлениеНаДействие("page404", "home");
	КонецЕсли;
КонецФункции

ИдентификаторСекции = "archive";