#Использовать "../../model"

Перем КаталогКонтента;
Перем Раздел;
Перем Подраздел;
Перем Страница;

Функция Index() Экспорт
	
	Раздел = ЭтотОбъект.ЗначенияМаршрута.Получить("section");	
	Подраздел = ЭтотОбъект.ЗначенияМаршрута.Получить("subsection");	
	Страница = ЭтотОбъект.ЗначенияМаршрута.Получить("id");
	
	Контент = Новый Структура("Содержимое, ЭтоMD", "", Истина);
	Меню = Новый Структура();
	
	Путь = "";
	Если ЭтоСтраница() Или ЭтоПодраздел() Тогда
		Каталог = ОбъединитьПути(КаталогКонтента, Раздел, Подраздел);
	ИначеЕсли ЭтоРаздел() Тогда
		Каталог = ОбъединитьПути(КаталогКонтента, Раздел);
	Иначе
		Каталог = "";
	КонецЕсли;

	Если Не ФС.КаталогСуществует(Каталог) Тогда
		Возврат ПеренаправлениеНаДействие("page404", "home");
	КонецЕсли;

	ПутьКФайлуКонтента = ПутьКФайлуКонтента(Каталог, ?(ЭтоСтраница(), Страница, "index"));
	Содержимое = "";
	Если Не ПустаяСтрока(ПутьКФайлуКонтента) Тогда	
		ТекстКонтента = МодульОбщегоНазначения.ПолучитьСодержимоеMDФайла(ПутьКФайлуКонтента, КодировкаТекста.UTF8);
		Содержимое = МодульОбщегоНазначения.ПреобразоватьMDВHTML(ТекстКонтента);	
	Иначе
		Если ЭтоСтраница() Тогда
			Возврат ПеренаправлениеНаДействие("page404", "home");
		КонецЕсли;
	КонецЕсли;	
	
	Объект = Новый Структура;
	Объект.Вставить("ЭтоMD", Истина);
	Объект.Вставить("Содержимое", Содержимое);
	Объект.Вставить("МенюКонтента", ДинамическоеУправлениеКонтентом.ПостроитьМеню(КаталогКонтента, Раздел, Подраздел, Страница));
	Возврат Представление("index-content", Объект);
	
КонецФункции

Функция ЭтоСтраница()
	Возврат Страница <> Неопределено;
КонецФункции

Функция ЭтоПодраздел()
	Возврат Подраздел <> Неопределено И Страница = Неопределено;
КонецФункции

Функция ЭтоРаздел()
	Возврат Раздел <> Неопределено И Подраздел = Неопределено И Страница = Неопределено;
КонецФункции

Функция ПутьКФайлуКонтента(Каталог, ИмяФайла = "index")
	Путь = "";
	Файлы = НайтиФайлы(Каталог, ИмяФайла + ".md");
	Если Файлы.Количество() > 0 Тогда
		Путь = Файлы[0].ПолноеИмя;
	КонецЕсли;
	Возврат Путь;
КонецФункции

КаталогКонтента = УправлениеКонтентом.КаталогКонтента();