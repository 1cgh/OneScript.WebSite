#Использовать "../../model"

Перем КаталогКонтента;
Перем Раздел;
Перем Подраздел;
Перем Страница;

Функция Index() Экспорт
	
	Раздел = ЭтотОбъект.ЗначенияМаршрута.Получить("section");	
	Подраздел = ЭтотОбъект.ЗначенияМаршрута.Получить("subsection");	
	Страница = ЭтотОбъект.ЗначенияМаршрута.Получить("id");
	
	Если ЭтоСтраница() Или ЭтоПодраздел() Тогда
		Каталог = ОбъединитьПути(КаталогКонтента, Раздел, Подраздел);
	ИначеЕсли ЭтоРаздел() Тогда
		Каталог = ОбъединитьПути(КаталогКонтента, Раздел);
	Иначе
		Каталог = "";
	КонецЕсли;
	
	Если Не ФС.КаталогСуществует(Каталог) Тогда
		Возврат ПеренаправлениеНаДействие("page404", "home");
	КонецЕсли;
	
	ПараметрыСтраницы = Новый ПараметрыСтраницыКонтекта();
	
	ПутьКФайлуКонтента = ПутьКФайлуКонтента(Каталог, ?(ЭтоСтраница(), Страница, "index"));
	Если Не ПустаяСтрока(ПутьКФайлуКонтента) Тогда	
		СодержимоеСтраницыКонтента = УправлениеКонтентом.СодержимоеСтраницыКонтента(ПутьКФайлуКонтента);
		ЗаполнитьЗначенияСвойств(ПараметрыСтраницы, СодержимоеСтраницыКонтента);
	Иначе
		Если ЭтоСтраница() Тогда
			Возврат ПеренаправлениеНаДействие("page404", "home");
		КонецЕсли;
	КонецЕсли;	

	ИнформацияОРазделе = ДинамическоеУправлениеКонтентом.ПостроитьСтруктуруРазделаКонтента(Раздел, Подраздел, Страница);

	ПараметрыСтраницы.НавигационноеМеню = ДинамическоеУправлениеКонтентом.БоковоеНавигационноеМеню(ИнформацияОРазделе.СтруктураРаздела);
	ЗаполнитьХлебныеКрошки(ПараметрыСтраницы, ИнформацияОРазделе);

	Если Не ЗначениеЗаполнено(ПараметрыСтраницы.Заголовок) Тогда
		ПараметрыСтраницы.Заголовок = "1Script - " + ИнформацияОРазделе.ЗаголовокРаздела;
	КонецЕсли;

	УправлениеКонтентом.УстановитьЗаголовокСтраницы(ЭтотОбъект, ПараметрыСтраницы.Заголовок);
	
	Возврат Представление("index-content", ПараметрыСтраницы);
	
КонецФункции

Процедура ЗаполнитьХлебныеКрошки(ПараметрыСтраницы, ИнформацияОРазделе)

	ЛокальныеПараметры = Новый ЛокальныеПараметрыСтраницы(ИнформацияОРазделе.ЗаголовокРаздела, ЭтоРаздел(), "/" + Раздел);
	ПараметрыСтраницы.ЛокальнаяНавигация.Добавить(ЛокальныеПараметры);
	
	Если ЗначениеЗаполнено(Подраздел) Тогда
		ЛокальныеПараметры = Новый ЛокальныеПараметрыСтраницы(ИнформацияОРазделе.ЗаголовокПодраздела, ЭтоПодраздел(), "/" + ОбъединитьПути(Раздел, Подраздел));
		ПараметрыСтраницы.ЛокальнаяНавигация.Добавить(ЛокальныеПараметры);
	КонецЕсли;

	Если ЗначениеЗаполнено(Страница) Тогда
		ЛокальныеПараметры = Новый ЛокальныеПараметрыСтраницы(ИнформацияОРазделе.ЗаголовокСтраницы, ЭтоСтраница());
		ПараметрыСтраницы.ЛокальнаяНавигация.Добавить(ЛокальныеПараметры);
	КонецЕсли;

КонецПроцедуры

Функция ЭтоСтраница()
	Возврат Страница <> Неопределено;
КонецФункции

Функция ЭтоПодраздел()
	Возврат Подраздел <> Неопределено И Страница = Неопределено;
КонецФункции

Функция ЭтоРаздел()
	Возврат Раздел <> Неопределено И Подраздел = Неопределено И Страница = Неопределено;
КонецФункции

Функция ПутьКФайлуКонтента(Каталог, ИмяФайла = "index")
	Путь = "";
	Файлы = НайтиФайлы(Каталог, ИмяФайла + ".md");
	Если Файлы.Количество() > 0 Тогда
		Путь = Файлы[0].ПолноеИмя;
	КонецЕсли;
	Возврат Путь;
КонецФункции

КаталогКонтента = УправлениеКонтентом.КаталогКонтента();