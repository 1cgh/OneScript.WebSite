#Использовать json
#Использовать fs

Функция ПостроитьМеню(КаталогКонтента, Раздел, Подраздел = "", Страница = "") Экспорт

	ЗаголовокРаздела = "";
	ЗаголовокПодраздела = "";
	ЗаголовокСтраницы = "";

	Каталог = ОбъединитьПути(КаталогКонтента, Раздел);
	ПутьКФайлуЗаголовков = ПутьКФайлуЗаголовков(Каталог);
	Таблица = ТаблицаНавигации();

	ТекущиеЗаголовкиРаздела = Новый ЗаголовкиКаталогаКонтента(ПутьКФайлуЗаголовков(КаталогКонтента));
	ЗаголовокРаздела = ТекущиеЗаголовкиРаздела.Заголовок(Раздел);

	Файлы = НайтиФайлы(Каталог, "*");
	ТекущиеЗаголовки = Новый ЗаголовкиКаталогаКонтента(ПутьКФайлуЗаголовков);
	

	Для Каждого Файл Из Файлы Цикл
		Если Файл.ЭтоКаталог() Тогда
			ИмяПодраздела = НРег(Файл.ИмяБезРасширения);
			Заголовок = ТекущиеЗаголовки.Заголовок(ИмяПодраздела);

			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Имя = ИмяПодраздела;
			НоваяСтрока.Порядок = ТекущиеЗаголовки.Порядок(ИмяПодраздела);
			Если НоваяСтрока.Порядок = Неопределено Тогда
				НоваяСтрока.Порядок = 999;
			КонецЕсли;
			НоваяСтрока.ПолноеИмя = Файл.ПолноеИмя;
			НоваяСтрока.Заголовок = ?(Заголовок = Неопределено, ИмяПодраздела, Заголовок);
			НоваяСтрока.Ссылка = ОбъединитьПути(Раздел, НоваяСтрока.Имя);
			НоваяСтрока.Активный = НоваяСтрока.Имя = Подраздел;
			Если НоваяСтрока.Активный Тогда
				ЗаголовокПодраздела = НоваяСтрока.Заголовок;
			КонецЕсли;
			НоваяСтрока.Страницы = СтраницыПодраздела(Каталог, НоваяСтрока, Страница, ЗаголовокСтраницы);
		КонецЕсли;
	КонецЦикла;

	Таблица.Сортировать("Порядок, Заголовок");

	Возврат БоковоеНавигационноеМеню(Таблица, ЗаголовокРаздела, ЗаголовокПодраздела, ЗаголовокСтраницы);

КонецФункции

Функция СтраницыПодраздела(Каталог, СтрокаНавигации, ТекущаяСтраница, ЗаголовокСтраницы)

	Таблица = ТаблицаНавигации();
	ТекущийКаталог = ОбъединитьПути(Каталог, СтрокаНавигации.Имя);
	ТекущиеЗаголовки = Новый ЗаголовкиКаталогаКонтента(ПутьКФайлуЗаголовков(ТекущийКаталог));

	Файлы = НайтиФайлы(ТекущийКаталог, "*.*");
	Для Каждого Файл Из Файлы Цикл
		Если НРег(Файл.ИмяБезРасширения) = "index" Тогда
			Продолжить;
		КонецЕсли;
		Если НРег(Файл.Расширение) = ".md" Тогда

			ИмяСтраницы = НРег(Файл.ИмяБезРасширения);
			Заголовок = ТекущиеЗаголовки.Заголовок(ИмяСтраницы);

			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Имя = ИмяСтраницы;
			НоваяСтрока.Порядок = ТекущиеЗаголовки.Порядок(ИмяСтраницы);
			Если НоваяСтрока.Порядок = Неопределено Тогда
				НоваяСтрока.Порядок = 999;
			КонецЕсли;
			НоваяСтрока.Подраздел = СтрокаНавигации.Имя;
			НоваяСтрока.Заголовок = ?(Заголовок = Неопределено, ИмяСтраницы, Заголовок);
			НоваяСтрока.ПолноеИмя = Файл.ПолноеИмя;
			НоваяСтрока.Ссылка = ОбъединитьПути(СтрокаНавигации.Ссылка, НоваяСтрока.Имя);
			НоваяСтрока.Активный = СтрокаНавигации.Активный И НоваяСтрока.Имя = ТекущаяСтраница;
			Если НоваяСтрока.Активный Тогда
				ЗаголовокСтраницы = НоваяСтрока.Заголовок;
			КонецЕсли;

		КонецЕсли;
	КонецЦикла;

	Таблица.Сортировать("Порядок, Заголовок");

	Возврат Таблица;

КонецФункции

Функция БоковоеНавигационноеМеню(Таблица, ЗаголовокРаздела, ЗаголовокПодраздела, ЗаголовокСтраницы)

	Меню = Новый БоковоеНавигационноеМеню();
	Меню.ЗаголовокРаздела = ЗаголовокРаздела;
	Меню.ЗаголовокПодраздела = ЗаголовокПодраздела;
	Меню.ЗаголовокСтраницы = ЗаголовокСтраницы;
	Для Каждого СтрокаПодраздела Из Таблица Цикл
		ЭлементМеню = Меню.ДобавитьРаздел(СтрокаПодраздела.Имя, СтрокаПодраздела.Заголовок, СтрокаПодраздела.Активный); 	
		СписокСтраниц = Новый Массив;
		Для Каждого Страница Из СтрокаПодраздела.Страницы Цикл
			СписокСтраниц.Добавить(Новый СтраницаНавигации(Страница.Имя, Страница.Заголовок, Страница.Ссылка, Страница.Активный));
		КонецЦикла;
		ЭлементМеню.Страницы = СписокСтраниц;
	КонецЦикла;
	Возврат Меню;

КонецФункции

Функция ПутьКФайлуЗаголовков(Каталог) Экспорт

	ПутьКФайлу = ОбъединитьПути(Каталог, "titles.json");
	Возврат ?(ФС.ФайлСуществует(ПутьКФайлу), ПутьКФайлу, "");

КонецФункции

Функция ТаблицаНавигации()

	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("Порядок");
	Таблица.Колонки.Добавить("Подраздел");
	Таблица.Колонки.Добавить("Заголовок");
	Таблица.Колонки.Добавить("ПолноеИмя");
	Таблица.Колонки.Добавить("Ссылка");
	Таблица.Колонки.Добавить("Активный");
	Таблица.Колонки.Добавить("Страницы");

	Возврат Таблица;

КонецФункции