#Использовать json
#Использовать fs

Функция ПостроитьМеню(КаталогКонтента, Раздел, Подраздел = "", Страница = "") Экспорт

	Каталог = ОбъединитьПути(КаталогКонтента, Раздел);
	ПутьКФайлуЗаголовков = ПутьКФайлуЗаголовков(Каталог);
	Таблица = ТаблицаНавигации();

	Файлы = НайтиФайлы(Каталог, "*");
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ЭтоКаталог() Тогда
			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Имя = НРег(Файл.ИмяБезРасширения);
			НоваяСтрока.ПолноеИмя = Файл.ПолноеИмя;
			НоваяСтрока.Заголовок = Файл.ИмяБезРасширения;
			НоваяСтрока.Ссылка = ОбъединитьПути(Раздел, НоваяСтрока.Имя);
			НоваяСтрока.Активный = НоваяСтрока.Имя = Подраздел;
			НоваяСтрока.Страницы = СтраницыПодраздела(Каталог, НоваяСтрока, Страница);
		КонецЕсли;
	КонецЦикла;

	ОтсортироватьТаблицуНавигации(Таблица, ПутьКФайлуЗаголовков);

	Возврат БоковоеНавигационноеМеню(Таблица);

КонецФункции

Функция СтраницыПодраздела(Каталог, СтрокаНавигации, ТекущаяСтраница)

	Таблица = ТаблицаНавигации();
	ТекущийКаталог = ОбъединитьПути(Каталог, СтрокаНавигации.Имя);
	ПутьКФайлуЗаголовков = ПутьКФайлуЗаголовков(ТекущийКаталог);

	Файлы = НайтиФайлы(ТекущийКаталог, "*.*");
	Для Каждого Файл Из Файлы Цикл
		Если НРег(Файл.ИмяБезРасширения) = "index" Тогда
			Продолжить;
		КонецЕсли;
		Если НРег(Файл.Расширение) = ".md" Тогда
			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Имя = НРег(Файл.ИмяБезРасширения);
			НоваяСтрока.Подраздел = СтрокаНавигации.Имя;
			НоваяСтрока.Заголовок = Файл.ИмяБезРасширения;
			НоваяСтрока.ПолноеИмя = Файл.ПолноеИмя;
			НоваяСтрока.Ссылка = ОбъединитьПути(СтрокаНавигации.Ссылка, НоваяСтрока.Имя);
			НоваяСтрока.Активный = СтрокаНавигации.Активный И НоваяСтрока.Имя = ТекущаяСтраница;
		КонецЕсли;
	КонецЦикла;

	ОтсортироватьТаблицуНавигации(Таблица, ПутьКФайлуЗаголовков);

	Возврат Таблица;

КонецФункции

Функция БоковоеНавигационноеМеню(Таблица)

	Меню = Новый БоковоеНавигационноеМеню();
	Для Каждого СтрокаПодраздела Из Таблица Цикл
		ЭлементМеню = Меню.ДобавитьРаздел(СтрокаПодраздела.Имя, СтрокаПодраздела.Заголовок, СтрокаПодраздела.Активный); 	
		СписокСтраниц = Новый Массив;
		Для Каждого Страница Из СтрокаПодраздела.Страницы Цикл
			СписокСтраниц.Добавить(Новый СтраницаНавигации(Страница.Имя, Страница.Заголовок, Страница.Ссылка, Страница.Активный));
		КонецЦикла;
		ЭлементМеню.Страницы = СписокСтраниц;
	КонецЦикла;
	Возврат Меню;

КонецФункции

Функция ПутьКФайлуЗаголовков(Каталог)

	ПутьКФайлу = ОбъединитьПути(Каталог, "titles.json");
	Возврат ?(ФС.ФайлСуществует(ПутьКФайлу), ПутьКФайлу, "");

КонецФункции

Процедура ОтсортироватьТаблицуНавигации(Таблица, ПутьКФайлуЗаголовков)

	Если Не ПустаяСтрока(ПутьКФайлуЗаголовков) Тогда
		ТекущиеЗаголовки = Новый ЗаголовкиКаталогаКонтента(ПутьКФайлуЗаголовков);
		Если ТекущиеЗаголовки.ЕстьЗаголовки() Тогда
			ЗаполнитьЗаголовкиНавигации(ТекущиеЗаголовки, Таблица);	
		КонецЕсли;
	КонецЕсли;
	Таблица.Сортировать("Заголовок");

КонецПроцедуры

Функция ТаблицаНавигации()

	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("Подраздел");
	Таблица.Колонки.Добавить("Заголовок");
	Таблица.Колонки.Добавить("ПолноеИмя");
	Таблица.Колонки.Добавить("Ссылка");
	Таблица.Колонки.Добавить("Активный");
	Таблица.Колонки.Добавить("Страницы");

	Возврат Таблица;

КонецФункции

Процедура ЗаполнитьЗаголовкиНавигации(ТекущиеЗаголовки, ТаблицаНавигации, КолонкаИмя = "Имя", КолонкаЗаголовок = "Заголовок")

	Для Каждого Заголовок Из ТекущиеЗаголовки.Заголовки Цикл
		Строка = ТаблицаНавигации.Найти(Заголовок.Ключ, КолонкаИмя);
		Если Строка <> Неопределено Тогда
			Строка[КолонкаЗаголовок] = Заголовок.Значение; 
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры