#Использовать json
#Использовать fs

Функция ПостроитьСтруктуруРазделаКонтента(Раздел, Подраздел = "", Страница = "") Экспорт
	
	ЗаголовокРаздела = "";
	ЗаголовокПодраздела = "";
	ЗаголовокСтраницы = "";
	
	КаталогКонтента = УправлениеКонтентом.КаталогКонтента();
	ТекущийКаталог = ОбъединитьПути(КаталогКонтента, Раздел);
	Таблица = ТаблицаНавигации();
	
	ЗаголовокРаздела = ЗаголовокРазделаИзОписанияСостава(ПутьКФайлуЗаголовков(КаталогКонтента), Раздел);
	ТекущееОписаниеСостава = Новый ОписаниеСоставаКаталогаКонтента(ПутьКФайлуЗаголовков(ТекущийКаталог));

	Файлы = НайтиФайлы(ТекущийКаталог, "*");
	Для Каждого Файл Из Файлы Цикл
		
		НоваяСтрока = СтрокаЭлементаКонтента(Таблица, Файл, ТекущееОписаниеСостава, Раздел);
		
		Если НоваяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока.Активный = НоваяСтрока.Имя = Подраздел;
		НоваяСтрока.Страницы = СтраницыПодраздела(ТекущийКаталог, НоваяСтрока, Страница, ЗаголовокСтраницы);
		
		Если НоваяСтрока.Активный Тогда
			ЗаголовокПодраздела = НоваяСтрока.Заголовок;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтсортироватьЭлементыКонтента(Таблица);
	
	Результат = Новый Структура;
	Результат.Вставить("СтруктураРаздела", 		Таблица);
	Результат.Вставить("ЗаголовокРаздела", 		ЗаголовокРаздела);
	Результат.Вставить("ЗаголовокПодраздела", 	ЗаголовокПодраздела);
	Результат.Вставить("ЗаголовокСтраницы", 	ЗаголовокСтраницы);
	
	Возврат Результат;
	
КонецФункции

Функция БоковоеНавигационноеМеню(Таблица) Экспорт
	
	Меню = Новый БоковоеНавигационноеМеню();
	Для Каждого СтрокаПодраздела Из Таблица Цикл
		ЭлементМеню = Меню.ДобавитьРаздел(СтрокаПодраздела.Имя, СтрокаПодраздела.Заголовок, СтрокаПодраздела.Активный); 	
		СписокСтраниц = Новый Массив;
		Для Каждого Страница Из СтрокаПодраздела.Страницы Цикл
			СписокСтраниц.Добавить(Новый СтраницаНавигации(Страница.Имя, Страница.Заголовок, Страница.Ссылка, Страница.Активный));
		КонецЦикла;
		ЭлементМеню.Страницы = СписокСтраниц;
	КонецЦикла;
	Возврат Меню;
	
КонецФункции

Функция ПутьКФайлуЗаголовков(Каталог) Экспорт
	
	ПутьКФайлу = ОбъединитьПути(Каталог, "titles.json");
	Возврат ?(ФС.ФайлСуществует(ПутьКФайлу), ПутьКФайлу, "");
	
КонецФункции

Процедура ОтсортироватьЭлементыКонтента(Таблица)
	Таблица.Сортировать("Порядок, Заголовок");
КонецПроцедуры

Функция ПорядокЭлементаКонтента(ТекущееОписаниеСостава, ИмяЭлемента)
	Порядок = ТекущееОписаниеСостава.Порядок(ИмяЭлемента);
	Если Порядок = Неопределено Тогда
		Порядок = 999;
	КонецЕсли;
	Возврат Порядок;
КонецФункции

Функция ЗаголовокЭлементаКонтента(ТекущееОписаниеСостава, ИмяЭлемента)
	Заголовок = ТекущееОписаниеСостава.Заголовок(ИмяЭлемента);
	Заголовок = ?(Заголовок = Неопределено, ИмяЭлемента, Заголовок);
	Возврат Заголовок;
КонецФункции

Функция ЗаголовокРазделаИзОписанияСостава(ПутьКФайлу, Раздел)
	ОписаниеСоставаРазделов = Новый ОписаниеСоставаКаталогаКонтента(ПутьКФайлу);
	Возврат ОписаниеСоставаРазделов.Заголовок(Раздел);
КонецФункции

Функция СтраницыПодраздела(Каталог, СтрокаНавигации, ТекущаяСтраница, ЗаголовокСтраницы)
	
	Таблица = ТаблицаНавигации();
	ТекущийКаталог = ОбъединитьПути(Каталог, СтрокаНавигации.Имя);
	ТекущееОписаниеСостава = Новый ОписаниеСоставаКаталогаКонтента(ПутьКФайлуЗаголовков(ТекущийКаталог));
	
	Файлы = НайтиФайлы(ТекущийКаталог, "*.*");
	Для Каждого Файл Из Файлы Цикл
		
		НоваяСтрока = СтрокаЭлементаКонтента(Таблица, Файл, ТекущееОписаниеСостава, СтрокаНавигации.Ссылка, Истина);
		
		Если НоваяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока.Активный = СтрокаНавигации.Активный И НоваяСтрока.Имя = ТекущаяСтраница;
		
		Если НоваяСтрока.Активный Тогда
			ЗаголовокСтраницы = НоваяСтрока.Заголовок;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтсортироватьЭлементыКонтента(Таблица);
	
	Возврат Таблица;
	
КонецФункции

Функция СтрокаЭлементаКонтента(Таблица, Файл, ТекущееОписаниеСостава, ЧастьСсылки, ЭтоУровеньСтраниц = Ложь)
	
	ИмяЭлементаКонтента = НРег(Файл.ИмяБезРасширения);

	Если Не ЭтоУровеньСтраниц И Не Файл.ЭтоКаталог() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЭтоУровеньСтраниц И (Не ЭтоФайлMD(Файл) Или ИмяЭлементаКонтента = "index") Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	НоваяСтрока 			= Таблица.Добавить();
	НоваяСтрока.Имя 		= ИмяЭлементаКонтента;
	НоваяСтрока.Порядок 	= ПорядокЭлементаКонтента(ТекущееОписаниеСостава, ИмяЭлементаКонтента);
	НоваяСтрока.Заголовок 	= ЗаголовокЭлементаКонтента(ТекущееОписаниеСостава, ИмяЭлементаКонтента);
	НоваяСтрока.ПолноеИмя 	= Файл.ПолноеИмя;
	НоваяСтрока.Ссылка 		= ОбъединитьПути(ЧастьСсылки, НоваяСтрока.Имя);
	
	Возврат НоваяСтрока;
	
КонецФункции

Функция ЭтоФайлMD(Файл)
	Возврат НРег(Файл.Расширение) = ".md";
КонецФункции

Функция ТаблицаНавигации()
	
	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("Порядок");
	Таблица.Колонки.Добавить("Подраздел");
	Таблица.Колонки.Добавить("Заголовок");
	Таблица.Колонки.Добавить("ПолноеИмя");
	Таблица.Колонки.Добавить("Ссылка");
	Таблица.Колонки.Добавить("Активный");
	Таблица.Колонки.Добавить("Страницы");
	
	Возврат Таблица;
	
КонецФункции