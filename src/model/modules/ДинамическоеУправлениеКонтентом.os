#Использовать json


Функция ПостроитьМеню(КаталогКонтента, Раздел, Подраздел = "", Страница = "") Экспорт

	// TODO: Сразу в боковое меню? Пока не понятно что с сортировкой

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("Заголовок");
	Таблица.Колонки.Добавить("ПолноеИмя");
	Таблица.Колонки.Добавить("Ссылка");
	Таблица.Колонки.Добавить("Активный");
	Таблица.Колонки.Добавить("Страницы");

	Каталог = ОбъединитьПути(КаталогКонтента, Раздел);
	ПутьКФайлуЗаголовков = "";
	Файлы = НайтиФайлы(Каталог, "*");
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ЭтоКаталог() Тогда
			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Имя = Файл.ИмяБезРасширения;
			НоваяСтрока.ПолноеИмя = Файл.ПолноеИмя;
			НоваяСтрока.Заголовок = Файл.ИмяБезРасширения;
			НоваяСтрока.Ссылка = ОбъединитьПути(Раздел, НоваяСтрока.Имя);
			// Сообщить(НоваяСтрока.Ссылка, СтатусСообщения.ОченьВажное);
			НоваяСтрока.Активный = НоваяСтрока.Имя = Подраздел;
			НоваяСтрока.Страницы = СтраницыПодраздела(Каталог, НоваяСтрока, Страница);
		Иначе
			Если Файл.Имя = "titles.json" Тогда
				ПутьКФайлуЗаголовков = Файл.ПолноеИмя;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если Не ПустаяСтрока(ПутьКФайлуЗаголовков) Тогда
		// указать заголовки, отсортировать
		Заголовки = ЗаголовкиКаталога(ПутьКФайлуЗаголовков);
		Для Каждого Заголовок Из Заголовки Цикл
			Строка = Таблица.Найти(Заголовок.Ключ, "Имя");
			Если Строка <> Неопределено Тогда
				Строка.Заголовок = Заголовок.Значение; 
			КонецЕсли;
		КонецЦикла;
		Таблица.Сортировать("Заголовок");
	КонецЕсли;

	Меню = Новый БоковоеНавигационноеМеню();
	Для Каждого СтрокаПодраздела Из Таблица Цикл
		ЭлементМеню = Меню.ДобавитьРаздел(СтрокаПодраздела.Имя, СтрокаПодраздела.Заголовок, СтрокаПодраздела.Активный); 	
		СписокСтраниц = Новый Массив;
		Для Каждого Страница Из СтрокаПодраздела.Страницы Цикл
			СписокСтраниц.Добавить(Новый СтраницаНавигации(Страница.Имя, Страница.Заголовок, Страница.Ссылка, Страница.Активный));
		КонецЦикла;
		ЭлементМеню.Страницы = СписокСтраниц;
	КонецЦикла;

	Возврат Меню;

КонецФункции

Функция СтраницыПодраздела(Каталог, СтрокаНавигации, ТекущаяСтраница)

	// Подраздел, Ссылка
	ТекущийКаталог = ОбъединитьПути(Каталог, СтрокаНавигации.Имя);
	//Сообщить("ТекущийКаталог: " + ТекущийКаталог, СтатусСообщения.ОченьВажное);
	ПутьКФайлуЗаголовков = "";

	Таблица = Новый ТаблицаЗначений();
	Таблица.Колонки.Добавить("Имя");
	Таблица.Колонки.Добавить("Подраздел");
	Таблица.Колонки.Добавить("Заголовок");
	Таблица.Колонки.Добавить("ПолноеИмя");
	Таблица.Колонки.Добавить("Ссылка");
	Таблица.Колонки.Добавить("Активный");

	Файлы = НайтиФайлы(ТекущийКаталог, "*.*");
	//Сообщить("Количество файлов: " + Файлы.Количество(), СтатусСообщения.ОченьВажное);
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ИмяБезРасширения = "index" Тогда
			Продолжить;
		КонецЕсли;
		Сообщить(Файл.Расширение, СтатусСообщения.ОченьВажное);
		Если Файл.Расширение = ".md" Тогда
			НоваяСтрока = Таблица.Добавить();
			НоваяСтрока.Имя = Файл.ИмяБезРасширения;
			НоваяСтрока.Подраздел = СтрокаНавигации.Имя;
			НоваяСтрока.Заголовок = Файл.ИмяБезРасширения;
			НоваяСтрока.ПолноеИмя = Файл.ПолноеИмя;
			НоваяСтрока.Ссылка = ОбъединитьПути(СтрокаНавигации.Ссылка, НоваяСтрока.Имя);
			Сообщить(НоваяСтрока.Ссылка, СтатусСообщения.ОченьВажное);
			НоваяСтрока.Активный = СтрокаНавигации.Активный И НоваяСтрока.Имя = ТекущаяСтраница;
		Иначе
			Если Файл.Имя = "titles.json" Тогда
				ПутьКФайлуЗаголовков = Файл.ПолноеИмя; 
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	// а надо?
	Таблица.Индексы.Добавить("Имя");

	Если Не ПустаяСтрока(ПутьКФайлуЗаголовков) Тогда
		// указать заголовки, отсортировать
		Заголовки = ЗаголовкиКаталога(ПутьКФайлуЗаголовков);
		Для Каждого Заголовок Из Заголовки Цикл
			Строка = Таблица.Найти(Заголовок.Ключ, "Имя");
			Если Строка <> Неопределено Тогда
				Строка.Заголовок = Заголовок.Значение; 
			КонецЕсли;
		КонецЦикла;
		Таблица.Сортировать("Заголовок");
	КонецЕсли;

	Возврат Таблица;

КонецФункции

Функция ЗаголовкиКаталога(ИмяФайла)

	Парсер = Новый ПарсерJSON();
	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, КодировкаТекста.UTF8);
	СтрокаJSON = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();
	ЗаголовкиФайл = Парсер.ПрочитатьJSON(СтрокаJSON);

	Заголовки = Новый Соответствие();
	Для Каждого Элемент Из ЗаголовкиФайл Цикл
		Заголовки.Вставить(Элемент.Получить("name"), Элемент.Получить("title"));	
	КонецЦикла;

	Возврат Заголовки;

КонецФункции